# Maintainer: K. Teichmann
options=("!debug")
pkgname=batocera-emulationstation
pkgver=41
pkgrel=1
pkgdesc="Emulationstation from batocera plus some scripts for fully working integration into Arch"
arch=('x86_64')
url="https://github.com/Ark-GameBox"
license=(
  'GPL-2.0-or-later'               #config files from batocera.linux
  'Apache-2.0' 'Ubuntu-font-1.0'   #fonts used in batocera-emulationstation
  'BSD-2-Clause'                   #copy of id3v2lib provided in repo of batocera-emulationstation
  'Zlib'                           #(modified) copy of nanosvg in repo of batocera-emulationstation
  'MIT'                            #batocera-linux, batocera-emulationstation, emulationstation-de, inbuilt library rcheevos, anything in this repo
)
depends=(
  #building of ES itself
  'sdl2_mixer' 'sdl2' 'libpulse'
  'rapidjson' 'boost' 'libvlc' 'freeimage' 'freetype2' 'pugixml'
  #PKGBUILD and emulator configuration
  'nodejs' 'yq'
)

# these are added to 'depends' in package()
_runtimeDependencies=(
  #required for emulator/game launching
  'fuse3'
  'fuse-overlayfs' # no-root overlays
  'squashfuse' 'fuseiso' 'bindfs' 'fuse-archive'
  # to hide controllers from games
  'firejail'
  # for os menu
  'jgmenu'
  # general control
  'antimicrox' 
)

makedepends=('cmake' 'git')
optdepends=(
  'batocera-es-theme-carbon: default theme as standalone package'
  'batocera-es-pacman: integrate batocera store with pacman (not implemented yet)'
  'unclutter: To hide the mouse when desired'
  'wine: for windows based games and emulators'
  'umu-launcher: alternative for windows based games and emulators'
  'winetricks: required for game isolation when wine/umu is used'
  'rsync: required to separately manage game updates and save games for wine games'
)

SRCDEST="./downloads"

# revisions are placed in a config file, which is created/updated when a new PKBUILD version is released
source revisions.conf
echo -e "\n\e[1m*** Effective source revisions ***\e[0m"
declare -p | grep -oE --color=none '_\w+_REVISION=.*'

source=(
  "batocera-emulationstation::git+https://github.com/batocera-linux/batocera-emulationstation.git#commit=${_BATOCERA_ES_REVISION}"
)
md5sums=('SKIP')

# retrieve source definitions from additional config files
echo -e "\n\e[1m*** Get all source definitions ***\e[0m"
for _cfg in sources*.conf; do
  echo " - [$_cfg]"
  source "$_cfg" || exit 1
done
unset _cfg
echo "Final sources list:"
printf ' - %s\n' "${source[@]}"

prepare(){
  cd "$srcdir/batocera-emulationstation"
  git submodule update --init

  cd external/id3v2lib
  #lib is linked statically, no need to install the object archives and headers
  installRemoved=$(cat src/CMakeLists.txt | grep -Ev '^INSTALL')
  echo "$installRemoved" > src/CMakeLists.txt
  
  cp -rf --preserve=timestamps "$srcdir/fs-root" "$SRCDEST" 

  local packageTarget="$SRCDEST/fs-root/opt/batocera-emulationstation"
  mkdir -p "$packageTarget"
  versionJson="{
  'package': '${pkgver}-${pkgrel}',
  'batocera-configs': '${_BATOCERA_REVISION}',
  'emulationstation': '${_BATOCERA_ES_REVISION}',
  'es-de-configs' : '${_ESDE_REVISION}'
}" 
  echo -e "${versionJson//\'/\"}" > "$packageTarget"/versions.json 
}

_generateConfig(){
  echo "generating config files from sources..."
  btcDir="$srcdir"/fs-root/opt/batocera-emulationstation
  targetFs="$SRCDEST"/fs-root

  FS_ROOT="$targetFs" "$btcDir"/btc-config generateGlobalConfig -v \
    --comment "Generated during PKGBUILD from git:batocera.linux:${_BATOCERA_REVISION}, git:batocera-emulationstation: ${_BATOCERA_ES_REVISION}"
  return $?
}

build(){
  # create configuration files for emulationstation
  _generateConfig
  
  # build ES itself
  cd "$srcdir/batocera-emulationstation"
  cmake -Wno-dev -S . -B . \
    -DENABLE_FILEMANAGER=ON -DDISABLE_KODI=ON -DENABLE_PULSE=ON -DUSE_SYSTEM_PUGIXML=ON \
    --install-prefix=/opt/batocera-emulationstation \
    -DCMAKE_C_FLAGS="-g0" -DCMAKE_CXX_FLAGS="-g0" -DCMAKE_BUILD_TYPE="Release" .

  make
}

package(){
  for (( i=0; i < "${#_runtimeDependencies[@]}"; i++ )); do
    source+=("${_runtimeDependencies[i]}")
  done

  srcRoot="$srcdir/batocera-emulationstation"
  cd "$srcRoot"
  export DESTDIR="$pkgdir/"
  make install/strip

  binPath="$pkgdir/opt/batocera-emulationstation/bin"

  #resources
  cp -r "$srcRoot/resources" "$binPath"

  #licenses from emulationstation repo, including libraries contained and build by batocera-emulationstation
  install -Dm0644 -T "$srcRoot/LICENSE.md" "$pkgdir/usr/share/licenses/$pkgname/MIT_batocera-emulationstation"
  install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" "$srcRoot"/*licen?e.txt
  install -Dm0644 -T "$srcRoot/external/id3v2lib/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/BSD-2-Clause_id3v2lib"
  install -Dm0644 -T "$srcRoot/external/libcheevos/rcheevos/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/MIT_rcheevos"
  install -Dm0644 -T "$srcRoot/external/nanosvg/nanosvg_license.txt" "$pkgdir/usr/share/licenses/$pkgname/Zlib_nanosvg"
  #license of the code placed in this PKGBUILD repo
  #install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" "$startdir/ABOUT-LICENSES"
  #install -Dm0644 -T "$startdir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/MIT_pkgbuild-additions"

  #List all (external) sources
  # FIXME: repair licence info
  #for (( i=0; i < "${#source[@]}"; i++ )); do
  #  _file="${source[i]%%::*}"
  #  _url="${source[i]#*::}"
  #  echo "- ${_file#rootfs}: $_url" >> "$pkgdir/usr/share/licenses/$pkgname/ABOUT-LICENSES"
  #done
  
  #localization
  mkdir -p "$pkgdir/usr"
  cp -rf "$binPath"/../share/* "$pkgdir/usr/"

  #copy config source files
  cp -rf "$SRCDEST"/fs-root/* "$pkgdir"
}
